configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs 
    
    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile{
        extendsFrom pluginLibs
    }
}
dependencies {
    //add lib/*.jar files to pluginLibs
    pluginLibs fileTree(dir: 'lib', include: '*.jar')
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    //include contents of output dir
    from "$buildDir/output"
    manifest {
        //create space-separated list of pluginLibs
        def libList = configurations.pluginLibs.collect{relativePath(it).replaceAll('\\\\','/')}.join(' ')
        attributes 'Rundeck-Plugin-Classnames': 'com.dtolabs.rundeck.plugin.windows.WindowsFileCopier,com.dtolabs.rundeck.plugin.windows.WindowsNodeExecutor', 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)
